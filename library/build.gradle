apply plugin: 'com.android.library'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'com.vanniktech.maven.publish'

android {
    compileSdkVersion 36
    namespace "com.eightbitlab.blurview"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 36
        renderscriptTargetApi 24
        renderscriptSupportModeEnabled true
        testInstrumentationRunner "de.mannodermaus.junit5.AndroidJUnit5Builder"
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'junit-jupiter'
                }
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compileOnly project(":renderscript")
    implementation 'androidx.annotation:annotation:1.9.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.4'
}

mavenPublishing {
    coordinates("com.huanli233.blurview", "blurview", "3.0.3")
    pom {
        name = 'BlurView'
        description = 'Dynamic iOS-like blur on Android'
        url = 'https://github.com/Dimezis/BlurView'

        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id = 'dimezis'
                name = 'Dmytro Saviuk'
            }
        }

        scm {
            connection = 'scm:git:github.com/Dimezis/BlurView.git'
            developerConnection = 'scm:git:ssh://github.com/Dimezis/BlurView.git'
            url = 'https://github.com/Dimezis/BlurView/tree/master'
        }
    }
}

task install(dependsOn: publishToMavenLocal) {
    group = 'publishing'
    description = 'Publishes the library to the local Maven repository.'
}
